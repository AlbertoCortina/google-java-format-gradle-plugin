plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.1'
}

version = '0.2-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        // gradle always loads its own groovy version, which leads to conflicts
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

//TODO(sherter): When ready to release next version configure travis to publish it (secure env)
pluginBundle {
    website = 'https://github.com/sherter/google-java-format-gradle-plugin'
    vcsUrl = 'https://github.com/sherter/google-java-format-gradle-plugin.git'
    description = 'Format your Java source files with google-java-format'
    tags = ['java', 'format', 'style']

    plugins {
        googleJavaFormatGradlePlugin {
            id = 'com.github.sherter.google-java-format'
            displayName = 'google-java-format gradle plugin'
        }
    }
}

if (version.endsWith('-SNAPSHOT')) {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId 'com.github.sherter.googlejavaformatgradleplugin'
                artifactId 'google-java-format-gradle-plugin'

                from components.java
            }
        }
        repositories {
            maven {
                name = 'SonatypeSnapshot'
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
                credentials {
                    username System.env.USERNAME
                    password System.env.PASSWORD
                }
            }
        }
    }
}
